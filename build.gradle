plugins {
    id 'idea'
    id 'com.palantir.docker' version '0.25.0' apply false
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://packages.confluent.io/maven/"
        }
        // confluent's schema registry client depends on 'com.github.everit-org.json-schema:org.everit.json.schema'.
        // the version, for some reason, is not stored on maven central, and mavenrepository.com says it is located
        // here.
        maven {
            url "https://repository.mulesoft.org/nexus/content/repositories/public"
        }
    }
}

subprojects {
    version = '1.0'
}


configure(subprojects.findAll { project -> file("${project.name}/src/main/java").exists() }) {

    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'eclipse'

    group = 'dev.buesing.ksd'
    description = "${project.name}"
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    idea {
        module {
            inheritOutputDirs = false
            outputDir = compileJava.destinationDir
            testOutputDir = compileTestJava.destinationDir
        }
    }

    dependencies {

        implementation ("org.apache.kafka:kafka-clients:${kafka_version}") {
            version {
                strictly kafka_version
            }
        }

        implementation "org.apache.commons:commons-lang3:${apache_commons_version}"
        //implementation "org.apache.commons:commons-text:${apache_commons_text_version}"
        //implementation "commons-io:commons-io:${apache_commons_io_version}"

        implementation "org.slf4j:slf4j-api:${slf4j_version}"
        implementation "ch.qos.logback:logback-classic:${logback_version}"
        implementation "com.beust:jcommander:${jcommander_version}"

        implementation "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
        implementation "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}"
        //implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jackson_version}"

        // lombok dependencies
        compileOnly "org.projectlombok:lombok:${lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${lombok_version}"

        testCompileOnly "org.projectlombok:lombok:${lombok_version}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"
    }

    test {
        useJUnitPlatform()
    }
}
